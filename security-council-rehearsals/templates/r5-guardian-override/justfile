set dotenv-load

export rpcUrl := env_var_or_default('ETH_RPC_URL', "https://ethereum.publicnode.com")
export signatures := env_var_or_default('SIGNATURES', '')
export councilSafe := env_var('COUNCIL_SAFE')
export bundle := "input"
export location := `pwd`
export randomPersonEoa := "0xd8dA6BF26964aF9D7eEd9e03E53415D37aA96045"
export dummyDgm := env_var('DUMMY_DEPUTY_GUARDIAN_MODULE')
export guardianSafe := env_var('GuardianSafe_ADDRESS')
export superchainConfig := env_var('SuperchainConfig_ADDRESS')
export etherscanKey := env_var_or_default('ETHERSCAN_KEY','')

deploy-contracts hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  sender=$(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  forge build
  forge script DeployRehearsal5 --ffi \
    --fork-url ${rpcUrl} \
    --ledger --hd-paths "m/44'/60'/{{hdPath}}'/0/0" \
    --broadcast \
    --sender ${sender} \
    --verify --etherscan-api-key ${etherscanKey}

prepare-json:
  #!/usr/bin/env bash
  bytes32_councilSafe=$(cast to-uint256 ${councilSafe} | cast to-bytes32 $*)
  prevalidatedSignature="${bytes32_councilSafe}"000000000000000000000000000000000000000000000000000000000000000001
  sentinel_module=0x0000000000000000000000000000000000000001
  calldata_from_guardian_0=$(cast calldata "unpause()")
  calldata_from_security_council_0=$(cast calldata "execTransaction(address,uint256,bytes,uint8,uint256,uint256,uint256,address,address,bytes)" \
    ${superchainConfig} \
    0 \
    ${calldata_from_guardian_0} \
    0 \
    0 \
    0 \
    0 \
    0x0000000000000000000000000000000000000000 \
    0x0000000000000000000000000000000000000000 \
    ${prevalidatedSignature}
  )
  jq ".transactions[0].to |= \"${guardianSafe}\"" ./input.json > ./input.tmp && mv input.tmp input.json
  jq ".transactions[0].data |= \"${calldata_from_security_council_0}\"" ./input.json > ./input.tmp && mv input.tmp input.json

  calldata_from_guardian_1=$(cast calldata "disableModule(address,address)" ${sentinel_module} ${dummyDgm})
  calldata_from_security_council_1=$(cast calldata "execTransaction(address,uint256,bytes,uint8,uint256,uint256,uint256,address,address,bytes)" \
    ${guardianSafe} \
    0 \
    ${calldata_from_guardian_1} \
    0 \
    0 \
    0 \
    0 \
    0x0000000000000000000000000000000000000000 \
    0x0000000000000000000000000000000000000000 \
    ${prevalidatedSignature}
  )
  jq ".transactions[1].to |= \"${guardianSafe}\"" ./input.json > ./input.tmp && mv input.tmp input.json
  jq ".transactions[1].data |= \"${calldata_from_security_council_1}\"" ./input.json > ./input.tmp && mv input.tmp input.json
